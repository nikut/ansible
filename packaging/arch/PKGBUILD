# Maintainer: Michel Blanc <mblanc@erasme.org>

pkgname=ansible-git
pkgver=1.1.3403.g5cd97e8
pkgrel=1
pkgdesc='A radically simple IT automation'
arch=('any')
url='http://www.ansible.com'
license=('GPL3')
depends=('python2' 'python2-paramiko' 'python2-jinja' 'python2-yaml')
makedepends=('git' 'asciidoc' 'fakeroot')
optdepends=('python2-pyzmq: needed for fireball mode'
            'python2-pyasn1: needed for fireball mode'
            'python2-crypto: needed for fireball mode'
            'python2-keyczar: needed for fireball mode')
conflicts=('ansible')
provides=('ansible')
backup=('etc/ansible/ansible.cfg')
source=($pkgname::git://github.com/ansible/ansible.git)
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --long | sed 's/^v//;s/-/./g'
}

build() {
  cd $pkgname
  make PYTHON=python2
}

package() {
  cd $pkgname

  mkdir -p "$pkgdir/usr/share/ansible"
  cp -dpr --no-preserve=ownership ./library/* "$pkgdir/usr/share/ansible/"
  cp -dpr --no-preserve=ownership ./examples "$pkgdir/usr/share/ansible"

  python2 setup.py install -O1 --root="$pkgdir"

  install -D examples/ansible.cfg "$pkgdir/etc/ansible/ansible.cfg"

  install -D README.md "$pkgdir/usr/share/doc/ansible/README.md"
  install -D COPYING "$pkgdir/usr/share/doc/ansible/COPYING"
  install -D CHANGELOG.md "$pkgdir/usr/share/doc/ansible/CHANGELOG.md"
  install -D CONTRIBUTING.md "$pkgdir/usr/share/doc/ansible/CONTRIBUTING.md"
  install -D RELEASES.txt "$pkgdir/usr/share/doc/ansible/RELEASES.txt"

  install -D docs/man/man1/ansible.1 "$pkgdir/usr/share/man/man1/ansible.1"
  install -D docs/man/man1/ansible-playbook.1 "$pkgdir/usr/share/man/man1/ansible-playbook.1"
  install -D docs/man/man1/ansible-pull.1 "$pkgdir/usr/share/man/man1/ansible-pull.1"
  install -D docs/man/man1/ansible-doc.1 "$pkgdir/usr/share/man/man1/ansible-doc.1"
}
